
//Перем НастройкиНумерации;

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПроизвольныйНомер = 21;
	ВариантНумерации = 1;
	ИмяДокумента = Параметры.ИмяДокумента;
	ПредставлениеДокумента = Параметры.ПредставлениеДокумента;
	
	ЭтотОбъект.АвтоЗаголовок = Ложь;
	ЭтотОбъект.Заголовок = "Настройка нумерации";
	
	БазовыеНастройки = РегистрыСведений.ун30_ОбъектыНумерации.БазовыеНастройки(Параметры.ИмяДокумента);
	
	Если НЕ БазовыеНастройки.Успех ИЛИ НЕ БазовыеНастройки.Включить Тогда
		Включить = Ложь;
		НастроитьВидимость();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораФорматаДаты();
	
	НастройкиНумерации = РегистрыСведений.ун30_ПравилаНумерации.ПравилаНумерации(Параметры.ИмяДокумента);
	
	ЗаполнитьФормуПоНастройкам();
	СФормироватьПримерНомера();
	
	Массив = Новый Массив;
	Массив.Добавить(Тип(СтрШаблон("ДокументСсылка.%1", ИмяДокумента)));
	Описание = Новый ОписаниеТипов(Массив);
	
	Элементы.ДокументПример.ОграничениеТипа = Описание;
	ДокументПример = Описание.ПривестиЗначение();
	
	НастроитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьПриИзменении(Элемент)
	
	НастройкиНумерации.Вставить("Включить", Включить);
	
	Если Включить Тогда
		ВключитьНаСервере();
		ЗаполнитьФормуПоНастройкам();
		СФормироватьПримерНомера();
	КонецЕсли;
	
	НастроитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ВключитьНаСервере()
	
	РегистрыСведений.ун30_ОбъектыНумерации.Включить(ИмяДокумента);
	
КонецПроцедуры



&НаКлиенте
Процедура ВариантПрефиксацииПриИзменении(Элемент)
	НастройкиНумерации.Вставить("ВариантНумерации", ВариантНумерации);
	НастроитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура РазделительДатыПриИзменении(Элемент)
	СформироватьПримерНомера();
КонецПроцедуры

&НаКлиенте
Процедура ВариантИспользованияДатыПриИзменении(Элемент)
	СформироватьПримерНомера();
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольныйНомерПриИзменении(Элемент)
	СФормироватьПримерНомера();
КонецПроцедуры

&НаКлиенте
Процедура РазделительПриИзменении(Элемент)
	СФормироватьПримерНомера();
КонецПроцедуры


#КонецОбласти



#Область КомандыФормы

&НаСервере
Процедура СохранитьНастройкиНаСервере(Ошибка)
	
	ЗаписатьБазовыеНастройки(Ошибка);
	
	Если Включить Тогда
		ЗаписатьНастройки(Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	Ошибка = Ложь;
	СохранитьНастройкиНаСервере(Ошибка);
	Если Не Ошибка Тогда
		Сообщить("Настройки записаны");
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура СброситьПорядковыйНомер(Команда)
	СброситьПорядковыйНомерНаСервере();
КонецПроцедуры

&НаСервере
Процедура СброситьПорядковыйНомерНаСервере()
	
	Данные = Новый Структура;
	Данные.Вставить("ИмяДокумента", ИмяДокумента);
	Данные.Вставить("ПорядковыйНомер", ПорядковыйНомер);
	
	РегистрыСведений.ун30_ТекущиеНомераДокументов.УстановитьПорядковыйНомер(Данные);

КонецПроцедуры


#КонецОбласти



#Область НастройкиНумерации

&НаСервере
Процедура ЗаписатьБазовыеНастройки(Ошибка)
	
	НастройкиНумерации.Вставить("Включить", Включить);
	НастройкиНумерации.Вставить("ВариантНумерации", ВариантНумерации);
	НастройкиНумерации.Вставить("ИмяДокумента", ИмяДокумента);
	НастройкиНумерации.Вставить("ПредставлениеДокумента", ПредставлениеДокумента);
	НастройкиНумерации.Вставить("Разделитель", Разделитель);
	НастройкиНумерации.Вставить("Перезаписывать", Перезаписывать);
	
	Попытка
		РегистрыСведений.ун30_ОбъектыНумерации.ЗаписатьНастройки(НастройкиНумерации);
	Исключение
		Ошибка = Истина;
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройки(Ошибка)
	
	УстановитьНастройкуФорматаДаты();
	
	Попытка
		РегистрыСведений.ун30_ПравилаНумерации.ЗаписатьНастройки(НастройкиНумерации);
	Исключение
		Ошибка = Истина;
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФормуПоНастройкам()
	
	Включить 			= БазовыеНастройки.Включить;
	Разделитель 		= БазовыеНастройки.Разделитель;
	Перезаписывать 		= БазовыеНастройки.Перезаписывать;
	ВариантНумерации 	= 1;
	
	Если БазовыеНастройки.ВариантНумерации <> 0 
		И ВариантНумерации <> БазовыеНастройки.ВариантНумерации Тогда
		
		ВариантНумерации = БазовыеНастройки.ВариантНумерации;
	КонецЕсли;
	
	Если ВариантНумерации = 1 Тогда
		ЗаполнитьНастройкиФорматаДаты();
	КонецЕсли;
	
	Данные = Новый Структура;
	Данные.Вставить("ИмяДокумента", ИмяДокумента);
	ПорядковыйНомер = РегистрыСведений.ун30_ТекущиеНомераДокументов.ПоследнийНомерДокумента(Данные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиФорматаДаты()
	
	ПериодНумерации = НастройкиНумерации.ПериодНумерации;
	
	НастройкиФорматаДаты = ун30_Сервер.ПолучитьНастройкиФорматаДаты(НастройкиНумерации.ФорматДаты);
	ФорматДаты 		= НастройкиФорматаДаты.ФорматДаты;
	РазделительДаты = НастройкиФорматаДаты.Разделитель;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкуФорматаДаты()
	
	Если ФорматДаты = 1 Тогда
		НастройкиНумерации.Вставить("ФорматДаты", СтрШаблон("10&01&21~%1", РазделительДаты));
	ИначеЕсли ФорматДаты = 2 Тогда
		НастройкиНумерации.Вставить("ФорматДаты", СтрШаблон("10&01~%1", РазделительДаты));
	КонецЕсли;
	
	НастройкиНумерации.Вставить("ПериодНумерации", ПериодНумерации);
	
КонецПроцедуры

#КонецОбласти



#Область ВспомогательныеМетоды

&НаСервере
Процедура НастроитьВидимость()
	
	Элементы.ВариантНумерации.Видимость 	= Включить;
	Элементы.Перезаписывать.Видимость 		= Включить;
	Элементы.ПримерНомера.Видимость 		= Включить;
	Элементы.ПроизвольныйНомер.Видимость 	= Включить;
	Элементы.Разделитель.Видимость 			= Включить;

	Элементы.ГруппаВариантПоДате.Видимость = Включить И ВариантНумерации = 1;
КонецПроцедуры


&НаСервере
Процедура СФормироватьПримерНомера()
	Если ВариантНумерации = 1 Тогда
		СформироватьПримерНомераПоДате();
	ИначеЕсли ВариантНумерации = 2 Тогда
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьПримерНомераПоДате() 
	
	БазовыеНастройки = Новый Структура;
	БазовыеНастройки.Вставить("ИмяДокумента", ИмяДокумента);
	БазовыеНастройки.Вставить("ДатаИсточника", ТекущаяДатаСеанса());
	БазовыеНастройки.Вставить("Разделитель", Разделитель);
	
	Данные = Новый Структура;
	Данные.Вставить("ФорматДаты", ФорматДаты);
	Данные.Вставить("Разделитель", РазделительДаты);
	Данные.Вставить("ПроизвольныйНомер", ПроизвольныйНомер);
	
	ПримерНомера = ун30_Сервер.СформироватьПримерНомераПоДате(Данные, БазовыеНастройки, 1);
	
КонецПроцедуры

&НаСервере
Процедура ПериодНумерацииПриИзмененииНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПериодНумерацииПриИзменении(Элемент)
	ПериодНумерацииПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДляДокументаПримера(Команда)
	Если Не Перезаписывать Тогда
		Сообщить("Для тестирования необходимо включить Перезаписывать");
		Возврат;
	КонецЕсли;
	ВыполнитьДляДокументаПримераНаСервере();
	ОповеститьОбИзменении(ДокументПример);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДляДокументаПримераНаСервере()
	ДокументОбъект = ДокументПример.ПолучитьОбъект();
	ДокументОбъект.Записать();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораФорматаДаты()

	Список = Новый СписокЗначений;
	Список.Добавить(1, "день.месяц.год");
	Список.Добавить(2, "месяц.год");
	
	Элементы.ФорматДаты.СписокВыбора = Список;

КонецПроцедуры

#КонецОбласти
