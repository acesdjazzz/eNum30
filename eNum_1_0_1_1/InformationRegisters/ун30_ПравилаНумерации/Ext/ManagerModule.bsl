
Процедура ЗаписатьНастройки(Настройки) Экспорт
	
	Базовые = Новый Массив;
	Базовые.Добавить("ИмяДокумента");
	Базовые.Добавить("ПредставлениеДокумента");
	Базовые.Добавить("Включить");
	Базовые.Добавить("ВариантНумерации");
	Базовые.Добавить("Разделитель");
	Базовые.Добавить("Перезаписывать");
	Базовые.Добавить("Успех"); // служебное
	
	Для каждого Настройка Из Настройки Цикл
		
		
		Если Базовые.Найти(Настройка.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Менеджер 				= РегистрыСведений.ун30_ПравилаНумерации.СоздатьМенеджерЗаписи();
		Менеджер.ИмяДокумента 	= Настройки.ИмяДокумента;
		Менеджер.Значение 		= Настройка.Значение;
		
		ИмяПД = СтрШаблон("Перечисление.ун30_НастройкиНумерации.%1", Настройка.Ключ);
		Менеджер.Настройка 		= ПредопределенноеЗначение(ИмяПД);
		
		Менеджер.Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры

Функция ПравилаНумерации(ИмяДокумента) Экспорт
	
	Результат = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ун30_ПравилаНумерации.Настройка.Порядок КАК Настройка,
	|	ун30_ПравилаНумерации.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ун30_ПравилаНумерации КАК ун30_ПравилаНумерации
	|ГДЕ
	|	ун30_ПравилаНумерации.ИмяДокумента = &ИмяДокумента";
	Запрос.УстановитьПараметр("ИмяДокумента", ИмяДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Ключ = Метаданные.Перечисления.ун30_НастройкиНумерации.ЗначенияПеречисления[Выборка.Настройка].Имя;
		Результат.Вставить(Ключ, Выборка.Значение);
	КонецЦикла;
	
	Если НЕ Результат.Свойство("ПериодНумерации") Тогда
		Результат.Вставить("ПериодНумерации", 1);
	КонецЕсли;
	Если НЕ Результат.Свойство("ФорматДаты") Тогда
		Результат.Вставить("ФорматДаты", "01&10~-");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции