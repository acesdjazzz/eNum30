// Параметры:
// Данные: "ИмяДокумента", "Номер"
Процедура ЗаписатьНомерДокумента(Данные) Экспорт
	
	Набор = РегистрыСведений.ун30_ТекущиеНомераДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.ИмяДокумента.Установить(Данные.ИмяДокумента);
	Набор.Прочитать();
	
	Если Набор.Количество() = 0 Тогда
		Запись = РегистрыСведений.ун30_ТекущиеНомераДокументов.СоздатьМенеджерЗаписи();
		Запись.ИмяДокумента 	= Данные.ИмяДокумента;
		Запись.Номер 			= Данные.Номер;
		Запись.ПорядковыйНомер 	= 1;
		Запись.Записать();
	Иначе 
		Набор[0].Номер = Данные.Номер;
		Набор.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПоследнийНомерДокумента(Данные) Экспорт
	
	Результат = Новый Структура("ПоследнийНомер", 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ун30_ТекущиеНомераДокументов.ПорядковыйНомер КАК ПорядковыйНомер
	|ИЗ
	|	РегистрСведений.ун30_ТекущиеНомераДокументов КАК ун30_ТекущиеНомераДокументов
	|ГДЕ
	|	ун30_ТекущиеНомераДокументов.ИмяДокумента = &ИмяДокумента";
	Запрос.УстановитьПараметр("ИмяДокумента", Данные.ИмяДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить("ПоследнийНомер", Выборка.ПорядковыйНомер);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

	
Процедура УвеличитьПорядковыйНомерДокумента(Данные) Экспорт
	
	Набор = РегистрыСведений.ун30_ТекущиеНомераДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.ИмяДокумента.Установить(Данные.ИмяДокумента);
	Набор.Прочитать();
	
	Если Набор.Количество() = 0 Тогда
		ПорядковыйНомер = 1;
		Номер = "";
	Иначе 
		ПорядковыйНомер = Набор[0].ПорядковыйНомер + 1;
		Номер = Набор[0].Номер;
	КонецЕсли;
	
	Менеджер = РегистрыСведений.ун30_ТекущиеНомераДокументов.СоздатьМенеджерЗаписи();
	Менеджер.ИмяДокумента 		= Данные.ИмяДокумента;
	Менеджер.ПорядковыйНомер 	= ПорядковыйНомер;
	Менеджер.Номер 				= Номер;
	
	Менеджер.Записать(Истина);
	
КонецПроцедуры

Процедура УстановитьПорядковыйНомер(Данные) Экспорт
	
	Набор = РегистрыСведений.ун30_ТекущиеНомераДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.ИмяДокумента.Установить(Данные.ИмяДокумента);
	Набор.Прочитать();
	
	Набор[0].ПорядковыйНомер = Данные.ПорядковыйНомер;
	
	Набор.Записать(Истина);
	
КонецПроцедуры
 
Функция ПолучитьСчетчик(Данные) Экспорт
	
	Результат = Новый Структура("Счетчик", 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ун30_ТекущиеНомераДокументов.Счетчик КАК Счетчик
	|ИЗ
	|	РегистрСведений.ун30_ТекущиеНомераДокументов КАК ун30_ТекущиеНомераДокументов
	|ГДЕ
	|	ун30_ТекущиеНомераДокументов.ИмяДокумента = &ИмяДокумента";
	Запрос.УстановитьПараметр("ИмяДокумента", Данные.ИмяДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить("Счетчик", Выборка.Счетчик);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьСчетчик(Данные) Экспорт
	
	Количество = 0;
	
	Набор = РегистрыСведений.ун30_ТекущиеНомераДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.ИмяДокумента.Установить(Данные.ИмяДокумента);
	Набор.Прочитать();
	Если Набор.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ #ИмяДокумента.Ссылка) КАК Количество
	|ИЗ
	|	Документ.#ИмяДокумента КАК #ИмяДокумента
	|ГДЕ
	|	#ИмяДокумента.Дата МЕЖДУ &НачДата И &КонДата";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента", Данные.ИмяДокумента);
	
	Запрос.УстановитьПараметр("НачДата", НачалоГода(Данные.Дата));
	Запрос.УстановитьПараметр("КонДата", КонецГода(Данные.Дата));
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Набор[0].Счетчик = Выборка.Количество;
	Набор.Записать(Истина);
	
КонецПроцедуры
