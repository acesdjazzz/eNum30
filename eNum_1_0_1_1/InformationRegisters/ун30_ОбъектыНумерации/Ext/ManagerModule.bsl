
Процедура ЗаписатьНастройки(Настройки) Экспорт
	
	Менеджер = РегистрыСведений.ун30_ОбъектыНумерации.СоздатьМенеджерЗаписи();
	//Для каждого Настройка Из Настройки Цикл
	//	Менеджер[Настройка.Ключ] = Настройка.Значение;
	//КонецЦикла;
	
	Менеджер.ИмяДокумента 			= Настройки.ИмяДокумента;
	Менеджер.ПредставлениеДокумента = Настройки.ПредставлениеДокумента;
	Менеджер.Включить 				= Настройки.Включить;
	Менеджер.ВариантНумерации 		= Настройки.ВариантНумерации;
	Менеджер.Разделитель 			= Настройки.Разделитель;
	Менеджер.Перезаписывать			= Настройки.Перезаписывать;
	Менеджер.Записать(Истина);
	
КонецПроцедуры

Процедура Включить(ИмяДокумента) Экспорт
	
	Набор = РегистрыСведений.ун30_ОбъектыНумерации.СоздатьНаборЗаписей();
	Набор.Отбор.ИмяДокумента.Установить(ИмяДокумента);
	Набор.Прочитать();
	Если Набор.Количество() = 0 Тогда
		Менеджер = РегистрыСведений.ун30_ОбъектыНумерации.СоздатьМенеджерЗаписи();
		Менеджер.Включить = Истина;
		
		Менеджер.ИмяДокумента = ИмяДокумента;
		Менеджер.ПредставлениеДокумента = Метаданные.Документы.Найти(ИмяДокумента).Представление();
		Менеджер.ВариантНумерации = 1;
		Менеджер.Разделитель = "-";
		Менеджер.Перезаписывать = Ложь;
		
		Менеджер.Записать();
	Иначе 
		Набор[0].Включить = Истина;
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция БазовыеНастройки(ИмяДокумента) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ун30_ОбъектыНумерации.Включить КАК Включить,
	|	ун30_ОбъектыНумерации.ВариантНумерации КАК ВариантНумерации,
	|	ун30_ОбъектыНумерации.Разделитель КАК Разделитель,
	|	ун30_ОбъектыНумерации.Перезаписывать КАК Перезаписывать
	|ИЗ
	|	РегистрСведений.ун30_ОбъектыНумерации КАК ун30_ОбъектыНумерации
	|ГДЕ
	|	ун30_ОбъектыНумерации.ИмяДокумента = &ИмяДокумента";
	Запрос.УстановитьПараметр("ИмяДокумента", ИмяДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("ИмяДокумента", 		ИмяДокумента);
		Результат.Вставить("Включить", 			Выборка.Включить);
		Результат.Вставить("ВариантНумерации", 	Выборка.ВариантНумерации);
		Результат.Вставить("Разделитель", 		Выборка.Разделитель);
		Результат.Вставить("Перезаписывать", 	Выборка.Перезаписывать);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

